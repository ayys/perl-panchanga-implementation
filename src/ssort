#!/usr/bin/perl

#----------------------------------------------------------------#
$description = "
Syntax: ssort [-u] filename

SSort sorts Sanskrit text in the CSX encoding. The sort order is
suitable only for Sanskrit (\"ç\" is a vowel, etc.). Only
lower-case text is supported! Anusvàra is sorted as if it were
homorganic nasal.

-u option does a \"unique\" sort, discarding multiple occurrences
   of the same item.
-h option prints this help.
";
#----------------------------------------------------------------#

use Getopt::Std;
getopts(':uh');
if ($opt_h) {
    print STDERR $description;
    exit 1;
}

%hnasals = ("k" => "ï",		# appropriate homorganic nasal for each stop
	    "g" => "ï",
	    "ï" => "ï",
	    "c" => "¤",
	    "j" => "¤",
	    "¤" => "¤",
	    "ñ" => "õ",
	    "ó" => "õ",
	    "õ" => "õ",
	    "t" => "n",
	    "d" => "n",
	    "n" => "n",
	    "p" => "m",
	    "b" => "m",
	    "m" => "m",
);

while (<>) {
    s/ü([kgïcj¤ñóõtdnpbm])/$hnasals{$1} . "\001" . $1/ge;	# \001 not used
    s/ai/E/g;							# in CSX
    s/au/O/g;
    s/([kgcjtdpb])h/\u$1/g;
    s/ñh/ò/g;
    s/óh/ô/g;
    tr[aàiãuåçéëíeEoOü§þkKgGïcCjJ¤ñòóôõtTdDnpPbBmyrlv÷ùsh]	# coerce to
      [ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwx];	# abc order
    push @list, $_;
}

@newlist = sort { clean($a) cmp clean($b) } @list;
undef @list;

foreach(@newlist) {
    tr[ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwx]
      [aàiãuåçéëíeEoOü§þkKgGïcCjJ¤ñòóôõtTdDnpPbBmyrlv÷ùsh];
    s/E/ai/g;
    s/O/au/g;
    s/([KGCJTDPB])/\l$1h/g;
    s/ò/ñh/g;
    s/ô/óh/g;
    s/.\001/ü/g;
    if ($opt_u) {
	unless ($_ eq $prev) {
	    print;
	    $prev = $_;
	}
    }
    else { print }
}

sub clean {						# Hide \001 and §
    $_ = shift;
    tr/\001P//d;
    return $_;
}
